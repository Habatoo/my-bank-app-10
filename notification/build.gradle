plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.cloud.contract' version '4.1.4'
    id 'org.springframework.boot' version '3.5.8'
}

jacoco {
    toolVersion = '0.8.11'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.5.8'
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2025.0.0'
    }
}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources.srcDir 'src/main/resources'
    }

    test {
        java {
            srcDir 'src/test/java'
            srcDir "$buildDir/generated-test-sources/contracts"
        }
        resources.srcDir 'src/test/resources'
    }

    notificationIntegrationTest {
        java.srcDir 'src/notificationIntegrationTest/java'
        resources.srcDir 'src/notificationIntegrationTest/resources'

        compileClasspath += sourceSets.main.output + configurations.testCompileClasspath
        runtimeClasspath += output + compileClasspath + configurations.testRuntimeClasspath
    }
}

configurations {
    notificationIntegrationTestImplementation.extendsFrom testImplementation
    notificationIntegrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    api project(':microservice-chassis')

    api 'org.springframework.boot:spring-boot-starter-oauth2-client'
    api 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    contractTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    testImplementation 'io.rest-assured:spring-web-test-client'

    notificationIntegrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    notificationIntegrationTestImplementation 'io.projectreactor:reactor-test'
    notificationIntegrationTestImplementation 'org.springframework.security:spring-security-test'

    notificationIntegrationTestImplementation 'org.testcontainers:postgresql'
    notificationIntegrationTestImplementation 'org.testcontainers:r2dbc'
    notificationIntegrationTestImplementation 'org.testcontainers:junit-jupiter'

    notificationIntegrationTestImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'
    notificationIntegrationTestImplementation 'com.squareup.okhttp3:mockwebserver'
    notificationIntegrationTestImplementation 'io.micrometer:micrometer-test'
    notificationIntegrationTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
    notificationIntegrationTestImplementation 'org.springframework.cloud:spring-cloud-contract-verifier'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

jar {
    enabled = false
}

bootJar {
    enabled = true
    archiveClassifier = ''
}

test {
    useJUnitPlatform()
    jvmArgs("-XX:+EnableDynamicAgentLoading")
}

tasks.register('notificationIntegrationTest', Test) {
    description = 'Runs notification integration tests'
    group = 'verification'

    testClassesDirs = sourceSets.notificationIntegrationTest.output.classesDirs
    classpath = sourceSets.notificationIntegrationTest.runtimeClasspath

    useJUnitPlatform()
    shouldRunAfter test
    dependsOn 'generateContractTests'
}

tasks.named('processNotificationIntegrationTestResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('processResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('processNotificationIntegrationTestResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('processTestResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

check.dependsOn notificationIntegrationTest

contracts {
    baseClassForTests = 'io.github.habatoo.controllers.BaseContractTest'
    testMode = 'WEBTESTCLIENT'
    contractsDslDir = file("$projectDir/src/test/resources/contracts")
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from components.java
            artifact tasks.named("verifierStubsJar")
        }
    }
}

processResources {
    from('src/main/resources') {
        include '**/*'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('bootJar') {
    requiresUnpack '**/db/changelog/*.yaml'
    from sourceSets.main.output
}

tasks.named('copyContracts') {
    mustRunAfter tasks.named('bootJar')
}

tasks.named('verifierStubsJar') {
    dependsOn tasks.named('bootJar')
}