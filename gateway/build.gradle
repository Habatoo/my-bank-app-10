plugins {
    id 'java-library'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.boot' version '3.5.8'
}

jacoco {
    toolVersion = '0.8.11'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.5.8'
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2025.0.0'
    }
}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources.srcDir 'src/main/resources'
    }

    test {
        java.srcDir 'src/test/java'
        resources.srcDir 'src/test/resources'
    }

    gatewayIntegrationTest {
        java.srcDir 'src/gatewayIntegrationTest/java'
        resources.srcDir 'src/gatewayIntegrationTest/resources'

        compileClasspath += sourceSets.main.output + configurations.testCompileClasspath
        runtimeClasspath += output + compileClasspath + configurations.testRuntimeClasspath
    }
}

configurations {
    gatewayIntegrationTestImplementation.extendsFrom testImplementation
    gatewayIntegrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    api project(':microservice-chassis')

    api 'org.springframework.boot:spring-boot-starter-thymeleaf'
    api 'org.springframework.boot:spring-boot-starter-oauth2-client'
    api 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    api 'org.springframework.cloud:spring-cloud-starter-gateway-server-webflux'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    gatewayIntegrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    gatewayIntegrationTestImplementation 'io.projectreactor:reactor-test'
    gatewayIntegrationTestImplementation 'org.springframework.security:spring-security-test'

    gatewayIntegrationTestImplementation 'org.testcontainers:postgresql'
    gatewayIntegrationTestImplementation 'org.testcontainers:r2dbc'
    gatewayIntegrationTestImplementation 'org.testcontainers:junit-jupiter'

    gatewayIntegrationTestImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'
    gatewayIntegrationTestImplementation 'com.squareup.okhttp3:mockwebserver'
    gatewayIntegrationTestImplementation 'io.micrometer:micrometer-test'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

jar {
    enabled = false
}

bootJar {
    enabled = true
    archiveClassifier = ''
}

test {
    useJUnitPlatform()
    jvmArgs("-XX:+EnableDynamicAgentLoading")
}

tasks.register('gatewayIntegrationTest', Test) {
    description = 'Runs gateway integration tests'
    group = 'verification'

    testClassesDirs = sourceSets.gatewayIntegrationTest.output.classesDirs
    classpath = sourceSets.gatewayIntegrationTest.runtimeClasspath

    useJUnitPlatform()
    shouldRunAfter test
}

tasks.named('processGatewayIntegrationTestResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('processResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('processTestResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

check.dependsOn gatewayIntegrationTest


processResources {
    from('src/main/resources') {
        include '**/*'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('bootJar') {
    requiresUnpack '**/*.yaml'
    from sourceSets.main.output
}