plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.cloud.contract' version '4.1.4'
}

jacoco {
    toolVersion = '0.8.11'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.5.8'
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2025.0.0'
    }
}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources.srcDir 'src/main/resources'
    }

    test {
        java {
            srcDir 'src/test/java'
            srcDir "$buildDir/generated-test-sources/contracts"
        }
        resources.srcDir 'src/test/resources'
    }

    accountIntegrationTest {
        java.srcDir 'src/accountIntegrationTest/java'
        resources.srcDir 'src/accountIntegrationTest/resources'

        compileClasspath += sourceSets.main.output + configurations.testCompileClasspath
        runtimeClasspath += output + compileClasspath + configurations.testRuntimeClasspath
    }
}

configurations {
    accountIntegrationTestImplementation.extendsFrom testImplementation
    accountIntegrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    api project(':microservice-chassis')

    api 'org.springframework.boot:spring-boot-starter-oauth2-client'
    api 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    api 'org.springframework.boot:spring-boot-starter-webflux'
    api 'org.springframework.boot:spring-boot-starter-security'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    contractTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    testImplementation 'io.rest-assured:spring-web-test-client'

    accountIntegrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    accountIntegrationTestImplementation 'io.projectreactor:reactor-test'
    accountIntegrationTestImplementation 'org.springframework.security:spring-security-test'
    accountIntegrationTestImplementation 'org.testcontainers:postgresql'
    accountIntegrationTestImplementation 'org.testcontainers:r2dbc'
    accountIntegrationTestImplementation 'org.testcontainers:junit-jupiter'
    accountIntegrationTestImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'
    accountIntegrationTestImplementation 'com.squareup.okhttp3:mockwebserver'
    accountIntegrationTestImplementation 'io.micrometer:micrometer-test'
    accountIntegrationTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
    accountIntegrationTestImplementation 'org.springframework.cloud:spring-cloud-contract-verifier'
    accountIntegrationTestImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

jar {
    enabled = true
    archiveClassifier = ''
}

test {
    useJUnitPlatform()
    jvmArgs("-XX:+EnableDynamicAgentLoading")
}

tasks.register('accountIntegrationTest', Test) {
    description = 'Runs Account integration tests'
    group = 'verification'

    testClassesDirs = sourceSets.accountIntegrationTest.output.classesDirs
    classpath = sourceSets.accountIntegrationTest.runtimeClasspath

    useJUnitPlatform()
    shouldRunAfter test
    dependsOn 'generateContractTests'
}

tasks.named('processAccountIntegrationTestResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('processResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('processTestResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

check.dependsOn accountIntegrationTest

contracts {
    testFramework = 'JUNIT5'
    baseClassForTests = 'io.github.habatoo.controllers.BaseAccountContractTest'
    testMode = 'WEBTESTCLIENT'
    contractsDslDir = file("$projectDir/src/test/resources/contracts")
}

sourceSets.contractTest {
    resources.srcDir 'src/main/resources'
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from components.java
            artifact tasks.named("verifierStubsJar")
        }
    }
}